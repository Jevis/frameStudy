apply plugin: 'com.android.application'

android {
    /*编译版本的SDK*/
    compileSdkVersion 23
    /*build tools的版本*/
    buildToolsVersion "23.0.3"

    signingConfigs {//签名信息
        release {
            storeFile file("../apk.keystore.jks")
            storePassword "zyb123"
            keyAlias "zbao"
            keyPassword "zyb123"
        }
    }

    defaultConfig {
        /*应用的包名*/
        applicationId "com.zbao.news"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        ndk {
            /*生成的so名字*/
            moduleName "news"
            /*输出指定三种abi体系结构下的so库*/
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }

    }
    /*多渠道打包配置*/
    productFlavors {
        umeng {} /*manifestPlaceholders = [ CHANNEL_NAME:"YINGYONGBAO"]  配置manifest中占位符配置*/
    }

    // java版本
    compileOptions {
        /*sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7*/
    }
    buildTypes {
        /*发布版*/
        release {
            signingConfig android.signingConfigs.release
            /*是否进行混淆*/
            minifyEnabled true
            /*混淆时配置文件的位置*/
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        /*debug版本*/
        debug {
            signingConfig android.signingConfigs.release
            /*是否进行混淆*/
            minifyEnabled false
            /*混淆时配置文件的位置*/
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // 移除lint检查的error
    lintOptions {
        abortOnError false
    }

    /*C 代码存放的文件目录*/
    sourceSets {
        main {
            jni.srcDirs = ['src/main/jni', 'src/main/jni/']
        }
    }
}

/*
 平台免入侵框架so库
native_dependencies {
    artifact 'com.taobao.dexposed:dexposed_l:0.2+:armeabi'
    artifact 'com.taobao.dexposed:dexposed:0.2+:armeabi'
}*/

dependencies {
    // 编译libs目录下的所有jar包
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:23.3.0'
    /*友盟统计分析*/
    compile 'com.umeng.analytics:analytics:latest.integration'
    /*扁平化设计支持控件*/
    compile 'com.android.support:design:23.3.0'
    /*网络访问框架 2个*/
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'
    /*Retrofit 使用Rxjava支持包*/
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0-beta4'
    /*Rxjava and RxAndroid 2个*/
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    /*android 新控件支持 3个*/
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    /*view注入框架*/
    compile 'com.jakewharton:butterknife:7.0.1'
    /*json解析*/
    compile 'com.google.code.gson:gson:2.4'
    /*图片加载框架  Fresco Glide*/
    compile 'com.squareup.picasso:picasso:2.5.2'
    /*log信息打印*/
    compile 'com.orhanobut:logger:1.3'
    /*事件总线框架*/
    compile 'com.squareup:otto:1.3.8'
    compile 'org.greenrobot:eventbus:3.0.0'
    /*透明状态栏实现*/
    compile 'com.jaeger.statusbaruitl:library:1.0.0'
    /*Mentrial Design  底部导航栏*/
    compile 'me.majiajie:pager-bottom-tab-strip:1.0.0'
    /*创建引导页*/
    compile 'com.github.chyrta:AndroidOnboarder:0.6'
    /*数据库ORM框架*/
    compile 'de.greenrobot:greendao:2.0.0'
    /*平台免入侵框架    腾讯X5浏览内核*/
    /*compile 'com.taobao.android:dexposed:0.1.1@aar'*/
}